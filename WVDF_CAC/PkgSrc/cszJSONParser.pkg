//-2WA
Use UI
Use szJSONParser.pkg
Use cszJSONObject.pkg

//  Class:          cszJSONParser
//  Author:         Michael Salzlechner
//  Copyright:      2016 StarZen Technologies, Inc.
//  Web:            www.starzen.com
//  Description:

Class cszJSONParser is a cComcszJSONParser
    
    Procedure Construct_Object
        Forward Send Construct_Object

    End_Procedure

    //  Function:       DeserializeString
    //  Author:         Michael Salzlechner
    //  Copyright:      2016 StarZen Technologies, Inc.
    //  Web:            www.starzen.com
    //  Description:    deserializes a string into a JSON object
    //                  on success this function will create and return a dynamic JSON object that needs to be destroyed
    //                  on failure the return value is 0
    
    Function DeserializeString String sJSON Returns Integer
        Variant vJSONObj
        
        Get ComDeserializeString sJSON to vJSONObj
        
        If (not(IsNullComObject(vJSONObj))) Begin
            Integer hoJSON
            Get Create (RefClass(cszJSONObject)) to hoJSON
            Set pvJSONObject of hoJSON to vJSONObj
            Set phoParser of hoJSON to (Self)
            Function_Return hoJSON
        End
        Else Function_Return 0
    End_Function
        
    //  Function:       NewJSONObject
    //  Author:         Michael Salzlechner
    //  Copyright:      2016 StarZen Technologies, Inc.
    //  Web:            www.starzen.com
    //  Description:    creates a new empty JSON object
    //                  on success this function will create and return a dynamic JSON object that needs to be destroyed
    //                  on failure the return value is 0
    
    Function NewJSONObject Returns Integer
        Variant vJSONObj 
        Get ComNewJSONObject to vJSONObj
        If (not(IsNullComObject(vJSONObj))) Begin
            Integer hoJSON
            Get Create (RefClass(cszJSONObject)) to hoJSON
            Set pvJSONObject of hoJSON to vJSONObj
            Set phoParser of hoJSON to (Self)
            Function_Return hoJSON
        End
        Function_Return 0
    End_Function

    //  Function:       NewJSONArray
    //  Author:         Michael Salzlechner
    //  Copyright:      2016 StarZen Technologies, Inc.
    //  Web:            www.starzen.com
    //  Description:    creates a new empty JSON Array
    //                  on success this function will create and return a dynamic JSON object that needs to be destroyed
    //                  on failure the return value is 0
    
    Function NewJSONArray Returns Integer
        Variant vJSONArr
        Get ComNewJSONArray to vJSONArr
        If (not(IsNullComObject(vJSONArr))) Begin
            Integer hoJSON
            Get Create (RefClass(cszJSONObject)) to hoJSON
            Set pvJSONObject of hoJSON to vJSONArr
            Set phoParser of hoJSON to (Self)
            Function_Return hoJSON
        End
        Function_Return 0
    End_Function

    //  Function:       XMLToJSON
    //  Author:         Michael Salzlechner
    //  Copyright:      2016 StarZen Technologies, Inc.
    //  Web:            www.starzen.com
    //  Description:    converts an XML string to a JSON string
    //                  on success this function will return string
    //                  on failure the return value is ""
    
    Function XMLToJSON String sXML Returns String
        String sJSON
        Get ComXMLToJSON sXML to sJSON
        Function_Return sJSON
    End_Function

    //  Function:       JSONToXML
    //  Author:         Michael Salzlechner
    //  Copyright:      2016 StarZen Technologies, Inc.
    //  Web:            www.starzen.com
    //  Description:    converts a JSON string to an XML string
    //                  on success this function will return string
    //                  on failure the return value is ""
    
    Function JSONToXML String sJSON Returns String
        String sXML
        Get ComJSONToXML sJSON to sXML
        Function_Return sXML
    End_Function
    
    Procedure End_Construct_Object
        Forward Send End_Construct_Object

    End_Procedure

End_Class

Object oParser is a cszJSONParser
End_Object //oParser
